fun not X
  return if X 
    then false 
    else true
end

fun min [A, B]
  return if A<B
    then A
    else B
end

fun max [A, B]
  return if A>B
    then A
    else B
end

fun arrayMax A
  N = size A
  Max = $A(1)
  for I from 1 to N do
    Max = max [Max, $A(I)]
  end
  return Max
end

fun arrayMin A
  N = size A
  Min = $A(1)
  for I from 1 to N do
    Max = min [Min, $A(I)]
  end
  return Max
end

fun check [Status, Message]
  if not Status then
    println ["Command failed:", Message]
  end
end

global AuctionRaw
global AuctionProduct
global Balances

global NPlayers
global ThisPlayer

global PrevRawCount
global PrevRawPrice
global PrevProductCount
global PrevProductPrice

global MyFutureFactories

fun onStart []
  println "Game started"
  ; Game-wise constants
  NPlayers = nPlayers[]
  ThisPlayer = thisPlayer[]
  ; Local arrays
  AuctionRaw = array NPlayers
  AuctionProduct = array NPlayers
  Balances = array NPlayers
  ; Track factories being built
  [MyFutureFactories] = player [thisPlayer[], ["Factories"]]
end

fun onTurn []
  NTransactions = nTransactions []

  ;====================== Header ==========================

  ; Print state
  println [NPlayers, "players in game"]
  for I from 1 to NPlayers do
    [Alive] = player [I, ["Alive"]]
    if Alive then
      println player [I, ["Name", "Balance", "Raw", "Product", "Factories"]]
    end
  end
  println ["Market:", marketState []]
  println ["It's me:", player [ThisPlayer, ["Name", "Balance", "Raw", "Product", "Factories"]]]

  ;====================== Game logic ==========================

  ; Get market state
  [RawCount, RawPrice, ProductCount, ProductPrice] = marketState []

  ; My status
  [MyBalance, MyRaw, MyProduct, MyFactories] = 
    player [ThisPlayer, ["Balance", "Raw", "Product", "Factories"]]

  ; Fetch balances
  NAlive = 0 
  NRicher = 0 ; have more money
  for I from 1 to NPlayers do
    [Alive, $Balances(I)] = player [I, ["Alive", "Balance"]]
    if not Alive then 
      $Balances(I) = 0 
    end else
      NAlive = NAlive + 1
      if $Balances(I) > MyBalance then
        NRicher = NRicher + 1
      end
    end
  end

  ; Check auctions
  for I from 1 to NPlayers do
    $AuctionRaw(I) = 0
    $AuctionProduct(I) = 0
  end
  for I from 1 to NTransactions do
    [Type, Player, Count, Price] = transaction I
    if Type = "AuctionRaw"    then $AuctionRaw(Player) = Price-PrevRawPrice end
    if Type = "AuctionProduct" then $AuctionProduct(Player) = Price-PrevProductPrice end
  end

  ; Act aggressively when balance is low
  DumpingRate = 1
  if NRicher > 0.5*NAlive then
    DumpingRate = 10
  end

  ; Best +/- DumpingRate
  ThisRawPrice = (arrayMin AuctionRaw) + DumpingRate + RawPrice
  ThisProductPrice = (arrayMin AuctionProduct) - DumpingRate + ProductPrice

  ThisRawCount = max [MyFactories-MyRaw, 0]
  ThisProductCount = min [MyProduct, ProductCount]

  println ["AuctionRaw:", AuctionRaw]
  println ["Will buy", ThisRawCount, "for", ThisRawPrice]
  println ["AuctionProduct:", AuctionProduct]
  println ["Will sell", ThisProductCount, "for", ThisProductPrice]


  ; Perform commands
  check buy [ThisRawCount, ThisRawPrice]
  check sell [ThisProductCount, ThisProductPrice]
  check produce 2

  if MyBalance > MyFutureFactories*25000 then
    check build 1
    MyFutureFactories = MyFutureFactories+1
  end

  ; Save for the next turn
  [PrevRawCount, PrevRawPrice, PrevProductCount, PrevProductPrice] = marketState []
end
